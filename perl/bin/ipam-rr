#!/usr/bin/perl

my $base_dir;
BEGIN {
  $base_dir = '/home/noc/IPAM';
  if (exists $ENV{IPAM_BASE}) {
    $base_dir = $ENV{IPAM_BASE};
  }
}
use strict;
use warnings;
use lib "$base_dir/lib/perl5";
use IPAM 0.01;
use Getopt::Long;
use NetAddr::IP;
use JSON;

my %opt;

sub usage() {
  print <<"EOF";
usage: $0 [--json [= pretty]] [--attribute=<attr>=<pattern> ... ] <SOURCE> <TYPE> [<TYPE> ...]
EOF
exit(1);
}

sub get_attribute($$) {
  my ($rr, $name) = @_;
  foreach my $attr ($rr->attributes()) {
    grep /$name/, keys(%{$attr}) or next;
    return (values(%{$attr}))[0];
  }
}

sub sort_rrs($@) {
  my ($type, @rrs) = @_;
  if ($type =~ /^(inet6?num|route6?)$/) {
    my $key = sub {
      my ($rr) =@_;
      my $sort_attr;
      if ($type =~ /^inet6?num$/) {
        $sort_attr = 'prefixes';
      } else {
        $sort_attr = $type;
      }
      my $key = (split(/\s+/, get_attribute($rr, $sort_attr)))[0];
      return NetAddr::IP->new($key);
    };
    return sort { $key->($a) <=> $key->($b) } @rrs;
  };
  return @rrs;
}

GetOptions(\%opt, "json:s", "attribute=s@") or usage();
(defined $opt{json} and ((not $opt{json} or $opt{json} eq 'pretty') or usage()));
@ARGV >= 2 or usage();
my $source = shift;
my @types = @ARGV;
my %predicates;
foreach (@{$opt{attribute}}) {
  (my ($attr, $pattern) = split(/=/)) == 2
    or usage();
  $predicates{$attr} = sub { shift =~ /$pattern/ };
}
system("cd $base_dir && make --no-print-directory --silent cache-maybe-rebuild") == 0 or die;
my $ipam = IPAM->new_from_cache("$base_dir/.ipam.cache");
my $rr_r = $ipam->registry(IPAM::REG_RR);

my @rrs;
map { push(@rrs,  sort_rrs($_, $rr_r->lookup_by_attributes($source, $_, \%predicates))) } @types;
if (defined $opt{json}) {
  my %json;
  map { push(@{$json{$_->type()}}, [ $_->attributes ]) } @rrs;
  print to_json(\%json, { pretty => $opt{json} eq 'pretty' ? 1 : 0 });
} else {
  map { print $_->dump(); print "\n" } @rrs;
}

## Local Variables:
## mode: CPerl
## End:
