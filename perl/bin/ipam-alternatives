#!/usr/bin/perl

my $base_dir;
BEGIN {
  $base_dir = '/home/noc/IPAM';
  if (exists $ENV{IPAM_BASE}) {
    $base_dir = $ENV{IPAM_BASE};
  }
}
use strict;
use warnings;
use lib "$base_dir/lib/perl5";
use IPAM 0.01;
use Getopt::Long;

my %opt = ();
my $indent_unit = 2;

sub indent($$);
sub show_alt($);

sub usage() {
  print <<"EOF";
usage: $0
EOF
  exit(1);
}

GetOptions(\%opt, 'detail')
  or usage();
system("cd $base_dir && make --silent .ipam.cache") == 0 or die;
my $ipam = IPAM->new_from_cache("$base_dir/.ipam.cache");
if (@ARGV) {
  foreach my $arg (@ARGV) {
    unless (my $alt = $ipam->registry(IPAM::REG_ALTERNATIVE)->lookup($arg)) {
      print STDERR "Unknown alternative \"$arg\".\n";
      print STDERR "Available alternatives: ".
	join(', ', map { $_->name() } 
	     $ipam->registry(IPAM::REG_ALTERNATIVE)->things())."\n";
      exit(1);
    } else {
      show_alt($alt);
    }
  }
} else {
  map { show_alt($_) } $ipam->registry(IPAM::REG_ALTERNATIVE)->things();
}

sub show_alt($) {
  my ($alt) = @_;
 indent(0, "\nAlternative \"".$alt->name."\":\n");
  my @states = $alt->allowed_states();
  my $state = $alt->state();
  indent(1, "Allowed states: ".join(', ', @states)."\n");
  indent(1, "Active state  : $state\n");
  indent(1, "TTL override  : ".(defined $alt->ttl() ? $alt->ttl() : '<none>')
	 ."\n");
  foreach my $state (@states) {
    my %hosts;
    indent(1,"Mappings for state \"$state\":\n");
    foreach my $ref ($alt->mappings($state)) {
      foreach my $type (IPAM::Alternative::MAP_ALIAS,
			IPAM::Alternative::MAP_ADDRESS,
			IPAM::Alternative::MAP_RR) {
	next unless exists $ref->{$type};
	### Sort by hostname, except for type alias, which is sorted
	### by the name of the alias.
	my $sub = sub { @$a[0]->name() cmp @$b[0]->name() };
	if ($type eq IPAM::Alternative::MAP_ALIAS) {
	  $sub = sub { @$a[1]->name() cmp @$b[1]->name() };
	}
	foreach my $mapping (sort $sub @{$ref->{$type}}) {
	  my ($host, $object) = @$mapping;
	  my @args;
	  if ($type eq IPAM::Alternative::MAP_ADDRESS) {
	      push(@args, 'Address', $host->name(), $object->name(),
		   info($host));
	  } elsif ($type eq IPAM::Alternative::MAP_ALIAS) {
	      push(@args, 'Alias  ', $object->name(), $host->name(),
		   info($host));
	  } elsif ($type eq IPAM::Alternative::MAP_RR) {
	      push(@args, 'Resource Record', $host->name(),
		   $object->{type}, info($host));
	  } else {
	    die "BUG: unknonwn alternative type $type";
	  }
	  indent(3, sprintf("%s: %-35s %-35s %s\n", @args));
	}
      }
    }
  }
}

sub indent($$) {
  my ($level, $msg) = @_;
  print ' 'x($level*$indent_unit).$msg;
}

sub info($) {
  my ($host) = @_;
  if ($opt{detail}) {
    my ($file, $line) = $host->nodeinfo();
    return("$file:$line");
  }
  return('');
}
